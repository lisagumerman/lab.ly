
def version = { deplist, version ->
    return deplist.collect {
        "$it:$version"
    }
}

project.ext {
    deps = [
            compile:[:],
            test:[:]
    ]
}


deps.compile.spring = version([
        "org.springframework:spring-core",
        "org.springframework:spring-tx",
        "org.springframework:spring-context",
        "org.springframework:spring-aop",
        "org.springframework:spring-orm",
], springVersion)

deps.compile.spring += [
        "org.springframework.data:spring-data-jpa:$springDataJpaVersion"
]

deps.test.spring = version([
        "org.springframework:spring-test"
], springVersion)

defaultTasks = [
        ":shared-config:clean",
        ":shared-config:build",
        "clean",
        "sharedConfiguration",
        "build"
]

allprojects {

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'

    configurations {
        sharedConfiguration
    }

    task sharedConfiguration(type:Copy) {
        from {
            project.configurations.sharedConfiguration.collect { zipTree(it)}
        }
        into("${project.buildDir}/classes/test")
        expand(project.properties)
    }

    test.mustRunAfter sharedConfiguration
    defaultTasks = ['clean', 'sharedConfiguration', 'build']


    dependencies {
        testCompile "junit:junit:$junitVersion"
    }

}
